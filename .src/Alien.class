' Gambas class file

Inherits Box

Public Sub _new()

  Me.Size.x = 0.5
  Me.Size.y = 0.5
  Me.Size.z = 0.5

End

Public Sub MoveForward()

  Me.z += 0.1

  If Me.z > 10 Then
    Me.z -= 100
  Endif

End

Public Sub CheckForHit(Screen As Screen)

  Dim Bullet As Bullet

  For Each Bullet In Screen.Bullets
    If Bullet.IntersectsXZ(Me) Then

      Try Screen.Aliens.Remove(Screen.Aliens.Find(Me, 0))
      Screen.Bullets.Remove(Screen.Bullets.Find(Bullet, 0))
      Screen.AlienDestroyed.Play()

    Endif
  Next

  If Me.IntersectsXZ(Screen.Ship) And Not Screen.Ship.Destroyed Then
    Screen.Ship.Destroyed = True
    Screen.ShipDestroyed.Play()
  Endif

End

Public Function Fire(Screen As Screen, MaxZ As Float)

  Dim Bullet As New Bullet

  Bullet.x = Me.x
  Bullet.z = MaxZ + 2
  Bullet.Velocity.z = 0.3
  Bullet.Cycle = Bullet.MaxCycle + 1
  Screen.AlienFire.Play()
  Screen.Bullets.Add(Bullet)

End

Public Function MinZ(Existing As Float) As Float

  If Me.z < Existing Then
    Return Me.z
  Else
    Return Existing
  Endif

End

Public Function MaxZ(Existing As Float) As Float

  If Me.z > Existing Then
    Return Me.z
  Else
    Return Existing
  Endif

End
