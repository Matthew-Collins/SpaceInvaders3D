' Gambas class file

Public Window As New Window(True) As "Window"
Public Camera As New Camera

Public Ship As New Ship
Public Aliens As New Alien[]
Public Bullets As New Bullet[]

Public Draws As Integer

Public Sub _new()

  Dim x As Integer
  Dim Alien As Alien

  With Window
    .Resize(1600, 1080)
    .Resizable = True
    .Text = "Space Invaders 3D"
  End With

  For x = 0 To 99
    Alien = New Alien
    With Alien
      .x = (x Mod 10) - 4.5
      .z = -100
      .z += x \ 10
    End With
    Aliens.Add(Alien)
  Next

End

Public Sub Window_Open()

  Me.Camera.Open(Me.Window)

End

Public Sub Window_Resize()

  Me.Camera.Resize(Me.Window)

End

Public Sub Window_Draw()

  Dim Bullet As Bullet
  Dim Alien As Alien

  Me.Camera.Update(Me.Window)

  If Not Ship.Destroyed Then
    Me.Ship.Draw()
  Endif

  For Each Bullet In Me.Bullets
    Bullet.Draw()
  Next

  For Each Alien In Me.Aliens
    Alien.CheckForHit(Me)
    Alien.MoveForward()
    Alien.Draw()
  Next

End

Public Sub Window_KeyPress()

  If Key.Code = Key.F1 Then Window.Fullscreen = Not Window.Fullscreen
  If Key.Code = key.Esc Then Window.Close()

  If Key.Code = Key.Left Then Ship.Velocity.x = -0.03
  If Key.Code = Key.Right Then Ship.Velocity.x = 0.03

  If Key.Code = Key.PageUp Then Camera.z -= 1
  If Key.Code = Key.PageDown Then Camera.z += 1

  If Key.Code = Key.Space Then Ship.Fire(Me)

  If Key.Code = Key.F5 Then Camera.Rotate.x += 1
  If Key.Code = Key.F6 Then Camera.Rotate.x -= 1
  If Key.Code = Key.F7 Then Camera.Rotate.y += 1
  If Key.Code = Key.F8 Then Camera.Rotate.y -= 1
  If Key.Code = Key.F9 Then Camera.y += 1
  If Key.Code = Key.F10 Then Camera.y -= 1
  If Key.Code = Key.F11 Then Camera.Angle += 1
  If Key.Code = Key.F12 Then Camera.Angle -= 1

End

Public Sub Window_KeyRelease()

  If Key.Code = Key.Left Then Ship.Velocity.x = 0
  If Key.Code = Key.Right Then Ship.Velocity.x = 0

End
