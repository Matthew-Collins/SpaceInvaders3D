' Gambas class file

Inherits Box

Public Screen As Screen

Public Destroyed As Boolean
Public MaxCycle As Integer = 25
Public Cycle As Integer = MaxCycle + 1
Public CanFire As Boolean = True
Public Firing As Boolean

Public Sub _new(Screen As Screen)

  Me.Screen = Screen

  Me.Size.x = 0.5
  Me.Size.y = 0.5
  Me.Size.z = 0.5

End

Public Sub Update()

  If Me.Cycle < MaxCycle
    Me.Cycle += 1
    Me.CanFire = False
  Else If Me.cycle = MaxCycle
    Me.Cycle += 1
    Me.CanFire = True
  Endif

  If Me.Firing Then
    Me.Fire
  Endif

End

Public Sub CheckForHit(Screen As Screen)

  Dim Bullet As Bullet

  For Each Bullet In Screen.Bullets
    If Bullet.IntersectsXZ(Me) Then

      Screen.Ship.Destroyed = True
      Screen.Bullets.Remove(Screen.Bullets.Find(Bullet, 0))
      Screen.ShipDestroyed.Play()

    Endif
  Next

End

Public Sub Fire()

  Dim Bullet As New Bullet

  If Not Me.Destroyed And Me.CanFire Then
    Me.Cycle = 0
    Bullet.x = Screen.Ship.x
    Bullet.z = -0.5
    Screen.Bullets.Add(Bullet)
    Screen.ShipFire.Play()
  Endif

End
